Import('env')

env.Append(
    CPPPATH = ['compat'],
    CPPDEFINES = dict(
        # Staticx cares only that extracted files have the correct
        # permission bits, namely executable. It doesn't care about
        # ownership or atime/mtime.
        LIBTAR_NO_SET_FILE_PERMS=1,
    ),
)

if 'DEBUG' in env['CPPDEFINES']:
    # We don't want libtar DEBUG
    del env['CPPDEFINES']['DEBUG']
else:
    env['CPPDEFINES']['LIBTAR_NO_OUTPUT'] = 1

libtar = env.StaticLibrary(
    target = 'tar',
    source = [
        'block.c',
        'extract.c',
        'handle.c',
        'util.c',
        'compat/strlcpy.c',
        'compat/strmode.c',
    ]
)

Return('libtar')
