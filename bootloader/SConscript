import sys
import staticx.constants as const
from buildutils import cquote

Import('env')

env.Append(
    CCFLAGS = ['-static'],
    LINKFLAGS = ['-static'],
    CPPPATH = [
        '#libtar',
        '#libxz',
    ],
    CPPDEFINES = {
        n: cquote(getattr(const, n)) for n in dir(const) if not n.startswith('_')
    },
)

bootloader = env.Program(
    target = 'bootloader',
    source = [
        'error.c',
        'elfutil.c',
        'extract.c',
        'main.c',
        'mmap.c',
        'util.c',
    ],
    LIBS = [
        'tar',
        'xz',
    ],
)

Return('bootloader')
